import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.7.21'
}

group = 'com.github.hashibiroko.spigot'
version = "0.0.1-1.19-R0.1-SNAPSHOT"

sourceCompatibility = 18
targetCompatibility = 18

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot:1.19-R0.1-SNAPSHOT'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.19.0'
    compileOnly 'org.yaml:snakeyaml:1.33'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'io.insert-koin:koin-core:3.3.0'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [
                'version': version.toString(),
                'api-version': '1.19',
        ]
    }
}

jar {
    exclude "META-INF/versions/9/module-info.class"
    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}

task copyJarToServer(type: Copy) {
    from jar
    into "server/plugins"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

assemble.dependsOn copyJarToServer